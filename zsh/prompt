# Set up colors
autoload colors zsh/terminfo
if [[ "$terminfo[colors]" -ge 8 ]]; then
  colors
fi
for color in RED GREEN YELLOW BLUE MAGENTA CYAN WHITE; do
  eval PR_$color='%{$terminfo[bold]$fg[${(L)color}]%}'
  eval PR_LIGHT_$color='%{$fg[${(L)color}]%}'
  (( count = $count + 1 ))
done
PR_NO_COLOR="%{$terminfo[sgr0]%}"

# Welcome message for login shells
if [[ $SHLVL -eq 1 ]] ; then
   echo
   print -P "$PR_GREEN Welcome to: $PR_BLUE%m"
   print -P "$PR_GREEN Running: $PR_BLUE`uname -srm`$PR_GREEN on $PR_BLUE%l"
   print -P "$PR_GREEN It is: $PR_BLUE%D{%r}$PR_GREEN on $PR_BLUE%D{%A %b %f %G}"
   echo
fi

# format titles for screen and rxvt
function title() {
  # escape '%' chars in $1, make nonprintables visible
  a=${(V)1//\%/\%\%}

  # Truncate command, and join lines.
  a=$(print -Pn "%40>...>$a" | tr -d "\n")

  case $TERM in
  screen)
    print -Pn "\ek$a:$3\e\\"      # screen title (in ^A")
    ;;
  xterm*|rxvt)
    print -Pn "\e]2;$2 | $a:$3\a" # plain xterm title
    ;;
  esac
}

# preexec is called just before any command line is executed
function preexec() {
  title "$1" "$USER@%m" "%35<...<%~"
}

function precmd() {
  PS1="[$PR_BLUE%n@%U%m%u$PR_NO_COLOR:$PR_RED%3c$PR_LIGHT_GREEN$(git_info_for_prompt)$PR_NO_COLOR]%(!.#.$) "
}

# if [[ -n $SSH_CONNECTION ]]; then
#   export PS1='%m:%3~$(git_info_for_prompt)%# '
# else
#   export PS1='%3~$(git_info_for_prompt)%# '
# fi